#!/usr/local/cpanel/3rdparty/bin/perl
# cpanel - find-latest-version                  Copyright(c) 2022 cPanel, L.L.C.
#                                                           All rights Reserved.
# copyright@cpanel.net                                         http://cpanel.net
# This code is subject to the cPanel license. Unauthorized copying is prohibited
#
# This provides incremental updates to existing packages in EasyApache4.
package ea_tomcat100::find_latest_version;

use strict;
use warnings;
use File::chdir;

use lib "../ea-tools/lib/ea4_tool";    # assumes ea-tools is checked out next to this repo
use ea4_tool::util ();

unless ( caller() ) {
    ea4_tool::util::find_latest_version( \&_get_required, \&_add_sum );
}

###############
#### helpers ##
###############

sub _get_required {
    my ($http) = @_;

    local $CWD = ea4_tool::util::get_path_of_repo("ea-tomcat100");
    my $specfile = ea4_tool::util::specfile($CWD);
    my $version  = ea4_tool::util::spec_get_version($specfile);

    my ($cur_minor) = reverse( split( /\./, $version, 3 ) );
    my $new_minor = $cur_minor + 2;

    my $res = $http->get("https://registry.hub.docker.com/v2/repositories/library/tomcat/tags/10.0.$new_minor");
    if ( !$res->{success} && !$res->{status} ne "404" ) {
        die "Could not GET tomcat 10.0 tag ($res->{status} $res->{reason})\n";
    }
    elsif ( $res->{success} ) {
        $version = "10.0.$new_minor";
    }

    my $name = "apache-tomcat-$version.tar.gz";
    my $url  = "https://downloads.apache.org/tomcat/tomcat-10/v$version/bin/$name";

    return ( $version, $url, $name );
}

sub _add_sum {
    my ( $http, $hr ) = @_;

    my $checksum_url = "https://www.apache.org/dist/tomcat/tomcat-10/v$hr->{version}/bin/$hr->{tarball}{name}.sha512";
    my $res          = $http->get($checksum_url);
    if ( !$res->{success} ) {
        die "Could not GET tomcat 10.0 SHASUMS page ($res->{status} $res->{reason})\n";
    }

    my $name     = $hr->{tarball}{name};
    my $checksum = ( $res->{content} =~ /^((?i)[0-9a-f]+)\s+\Q*$name\E$/m )[0];
    unless ($checksum) {
        die "No checksum for $name at $checksum_url:\nContent: $res->{content}\n";
    }

    $hr->{tarball}{sum}{hex}  = $checksum;
    $hr->{tarball}{sum}{type} = "sha512";

    return;
}

