#!/usr/local/cpanel/3rdparty/bin/perl

use strict;
use warnings;

package ea_tomcat100::get_ports;

use XML::LibXML;

script (@ARGV) if not caller ();

sub get_ports
{
    my ($fname) = @_;

    my $dom = XML::LibXML->load_xml (location => $fname);
    my @connectors = ( $dom->findnodes ('/Server/Service/Connector') );

    my $http_port = -1;
    my $ajp_port = -1;

    for my $connector (@connectors) {
        my $protocol = $connector->getAttribute ("protocol");
        if (index ($protocol, 'HTTP') == 0) {
            my $port = $connector->getAttribute ("port");
            $http_port = $port;
        }
        elsif (index ($protocol, 'AJP') == 0) {
            my $port = $connector->getAttribute ("port");
            $ajp_port = $port;
        }
    }

    return "$http_port:$ajp_port";
}

sub script
{
    my (@args) = @_;

    die "Pass in homedir" if !@args;

    my $homedir = $args [0];
    die "Pass in homedir" if !-e $homedir;

    my $fname = $homedir . "/ea-tomcat100/conf/server.xml";

    if (!-e $fname) {
        print "Error";
        exit;
    }

    my $port = get_ports ($fname);
    print $port;
    
    return 0;
}

1;

