#!/usr/local/cpanel/3rdparty/bin/perl

use strict;
use warnings;

package ea_tomcat100::tomcat_access;

use XML::LibXML;

script(@ARGV) if not caller();

sub get_ports {
    my ($server_xml) = @_;

    my $dom        = XML::LibXML->load_xml( location => $server_xml );
    my @connectors = ( $dom->findnodes('/Server/Service/Connector') );

    my $http_port = -1;
    my $ajp_port  = -1;

    for my $connector (@connectors) {
        my $protocol = $connector->getAttribute("protocol");
        if ( index( $protocol, 'HTTP' ) == 0 ) {
            my $port = $connector->getAttribute("port");
            $http_port = $port;
        }
        elsif ( index( $protocol, 'AJP' ) == 0 ) {
            my $port = $connector->getAttribute("port");
            $ajp_port = $port;
        }
    }

    return "$http_port:$ajp_port";
}

sub script {
    my (@args) = @_;

    my $homedir;

    if ( $< == 0 ) {
        die "Pass in homedir" if !@args;
        $homedir = $args[0];
    }
    else {
        $homedir = $ENV{HOME};
    }

    die "Could not determine homedir" if !-e $homedir;

    my $fname = $homedir . "/ea-tomcat100/conf/server.xml";

    if ( !-e $fname ) {
        print "Error";
        exit;
    }

    my $port = get_ports($fname);
    print $port;

    return 0;
}

sub logit
{
    my ($msg) = @_;

    my $homedir = $ENV{HOME};
    if (open my $fh, ">>", "$homedir/ea-tomcat100/logs/tomcat_podman.log") {
        print $fh "$$: " . $msg . "\n";
        close $fh;
    }

    return;
}

1;

